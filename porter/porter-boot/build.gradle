group 'cn.vbill.middleware.porter'
apply plugin: 'application'
archivesBaseName = 'porter-node-boot'
mainClassName = "cn.vbill.middleware.porter.boot.NodeBootApplication"

applicationDefaultJvmArgs = ["-Dapp.home=DEFINE_APP_HOME/", "-Xms256M", "-Xmx256M"]

sourceSets {
    main {
    }
}
processResources {
    exclude(
           // "**/**"
    )
}

task moveOutFromJar(type: Copy) {
    sourceSets.main.resources.srcDirs.each {
        from it
        into buildDir.getPath() + "/config"
    }
}

processResources.dependsOn moveOutFromJar



distributions {
    main {
        contents {
            from ("script") {
                into "bin"
                fileMode = 0755
            }
            from (buildDir.getPath() + "/config") {
                into "config"
            }

            from ("plugins") {
                into "plugins"
            }
        }
    }
}

startScripts {
    doLast {
        unixScript.text = unixScript.text.replace('CLASSPATH=', 'cd $APP_HOME;\nCLASSPATH=').replace("DEFINE_APP_HOME",'$APP_HOME')
        windowsScript.text = windowsScript.text.replace('set CLASSPATH=', 'cd %APP_HOME%\nset CLASSPATH=').replace("DEFINE_APP_HOME",'%APP_HOME%')
    }
}


dependencies {
    compile(
            project(":porter:porter-cluster"),
            project(":porter:porter-task"),
            "org.springframework.boot:spring-boot-starter-web:${springVersion}"
    )
}
