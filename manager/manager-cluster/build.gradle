group 'cn.vbill.middleware.porter.manager'
apply plugin: 'application'
archivesBaseName = 'porter-manager-cluster'
mainClassName = "cn.vbill.middleware.porter.manager.ManagerClusterApplication"
ext {
    runJars=archivesBaseName + "-" + version + ".jar"
    runJarsPlaceHolder="{{RUN_JAR}}"
    mainClassPlaceHolder="{{MAIN_CLASS}}"
}

sourceSets {
    main {
    }
}
processResources {
    exclude(
            // "**/**"
    )
}

task moveOutFromJar(type: Copy) {
    sourceSets.main.resources.srcDirs.each {
        from it
        into buildDir.getPath() + "/config"
    }
}

jar {
    doFirst {
        manifest {
            if (!configurations.compile.isEmpty()) {
                attributes(
                        'Class-Path': configurations.compile.collect{it.getName()}.join(' '),
                        'Main-Class': mainClassName)
            }
        }
    }
}


processResources.dependsOn moveOutFromJar


startScripts {
    doLast {
        unixScript.text = resources.text.fromFile('script/unixStart.sh').asString()
                .replace(runJarsPlaceHolder, runJars).replace(mainClassPlaceHolder, mainClassName)
        windowsScript.text = resources.text.fromFile('script/windowsStart.bat').asString()
                .replace(runJarsPlaceHolder, runJars).replace(mainClassPlaceHolder, mainClassName)
    }
}

distributions {
    main {
        contents {
            from (buildDir.getPath() + "/config") {
                into "config"
            }
        }
    }
}


dependencies {
    compile project(':manager:manager-core')
    compile "org.springframework.boot:spring-boot-starter:${springVersion}"
    compile("org.springframework.kafka:spring-kafka:1.3.7.RELEASE")
    testCompile "org.springframework.boot:spring-boot-starter-actuator:${springVersion}"
    testCompile "org.springframework.boot:spring-boot-starter-test:${springVersion}"
}